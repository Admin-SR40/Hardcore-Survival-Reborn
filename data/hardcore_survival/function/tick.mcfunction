#   _   _               _                      ____                   _            _ 
#  | | | | __ _ _ __ __| | ___ ___  _ __ ___  / ___| _   _ _ ____   _(___   ____ _| |
#  | |_| |/ _` | '__/ _` |/ __/ _ \| '__/ _ \ \___ \| | | | '__\ \ / | \ \ / / _` | |
#  |  _  | (_| | | | (_| | (_| (_) | | |  __/  ___) | |_| | |   \ V /| |\ V | (_| | |
#  |_| |_|\__,_|_|  \__,_|\___\___/|_|  \___| |____/ \__,_|_|    \_/ |_| \_/ \__,_|_|
#                           ____      _                                              
#                          |  _ \ ___| |__   ___  _ __ _ __                          
#                          | |_) / _ | '_ \ / _ \| '__| '_ \                         
#                          |  _ |  __| |_) | (_) | |  | | | |                        
#                          |_| \_\___|_.__/ \___/|_|  |_| |_|                        
#

# Hardcore Survival Reborn
# Created by Admin_SR40
# Lisensed under the GPLv3.0

# Thanks for playing Hardcore Survival Reborn!
# Current Version: 2.3 Gamma
# Compatible Minecraft Version: 1.21.7+
# GitHub Respository: https://github.com/Admin-SR40/Hardcore-Survival-Reborn

# Found bugs? Create an Issue on GitHub or email me at 402757046@qq.com!
# Sorry, but all comments are in Chinese.

# This is the main file of Hardcore Survival Reborn, if you want to modify it, you can visit the wiki page.
# Wiki Page: https://deepwiki.com/Admin-SR40/Hardcore-Survival-Reborn

# (The wiki is generated by AI, which may contain errors.)
# (You can ask the AI to get examples of how to modify the code.)
# (Remember to ask to refresh the page to get the latest and most accurate information.)

# Coded with Visual Studio Code
# Extensions Used: Datapack Helper Plus, Syntax-mcfunction, GitHub Copilot

# Contributors (IGN + UUID):
#  - Admin_SR40                      (eb914b7c-e7fe-4b47-a386-ef1e274c4f5c)
#  - FishHave                        (7efe41f6-121a-4381-84bd-6ded6251c11f)
#  - BaiYu_                          (b841d922-1633-4093-9fe2-7eb7f81b8682)
#  - Hqn233                          (9417dc45-aee9-478f-8d34-eb5082d7cf33)
#  - SuererRiels                     (14b36930-385b-4f46-9f78-e8df0d1a2a24)
#  - shuishengOVO                    (44108a47-81fe-4b26-b38a-3211a9fd677d)

### 受击后受到伤害增加 ###
execute as @a if score @s hs.takeDamage matches 30.. if data entity @s {HurtTime:9s} run function hardcore_survival:wound/take
execute as @a if score @s hs.woundLevel matches 1.. run scoreboard players add @s hs.woundTimer 1
execute as @a if score @s hs.woundLevel matches 1.. if score @s hs.woundTimer matches 600.. run scoreboard players remove @s hs.woundLevel 1
execute as @a unless score @s hs.woundLevel matches 1.. run scoreboard players set @s hs.woundTimer 0

### 死亡重置 ###
execute as @a if score @s hs.death matches 1.. run function hardcore_survival:death/death

### 锁定难度 ###
difficulty hard

### 设定计分板初始值 ###
scoreboard players set @a hs.percentage 100
execute as @a store result score @s hs.maxHealth run attribute @s max_health get
execute as @a store result score @s hs.yaw run data get entity @s Rotation[0]
execute as @a store result score @s hs.pitch run data get entity @s Rotation[1]
execute as @e[type=!#ignore] if predicate hardcore_survival:fire store result score @s hs.fireTick run data get entity @s Fire

### 新玩家初始化 ###
execute as @a if entity @s[tag=!hs.init] run function hardcore_survival:init/init

### 水分减少 ###
execute as @a if score @s hs.water matches 1.. run scoreboard players add @s hs.waterTimer 2
execute as @a if score @s hs.sprintTick matches 1.. run scoreboard players add @s hs.waterTimer 2
execute as @a if score @s hs.jumpTimes matches 1.. run scoreboard players add @s hs.waterTimer 20
execute as @a if score @s hs.waterTimer matches 1440.. run scoreboard players remove @s hs.water 1
execute as @a if score @s hs.waterTimer matches 1440.. run scoreboard players set @s hs.waterTimer 0
execute as @a if score @s hs.water matches 0 run scoreboard players set @s hs.waterTimer 0
execute as @a if score @s hs.water matches 0 run scoreboard players add @s hs.waterHurtTimer 1
execute as @a if score @s hs.waterHurtTimer matches 80.. run damage @s 1 bypass
execute as @a if score @s hs.waterHurtTimer matches 80.. run scoreboard players set @s hs.waterHurtTimer 0
execute as @a if score @s hs.water matches 1.. run scoreboard players set @s hs.waterHurtTimer 0

### 水中蹲下喝水 ###
execute as @a at @s if score @s hs.sneakTick matches 1.. if block ~ ~ ~ water run function hardcore_survival:drink/sneak

### 黑暗地区获得黑暗BUFF ###
execute as @a at @s if predicate hardcore_survival:is_dark run scoreboard players add @s hs.darkTimer 1
execute as @a at @s unless predicate hardcore_survival:is_dark run scoreboard players remove @s hs.darkTimer 1
execute as @a if score @s hs.darkTimer matches ..-601 run scoreboard players set @s hs.darkTimer -600
execute as @a if score @s hs.darkTimer matches 601.. run scoreboard players set @s hs.darkTimer 600
execute as @a at @s if predicate hardcore_survival:is_dark unless dimension the_end unless data entity @s {active_effects:[{id:"minecraft:night_vision"}]} if score @s hs.darkTimer matches ..-1 run effect give @s darkness 3 0 true

### 低头显示水分 ###
execute as @a if score @s hs.pitch matches 61..90 run title @s actionbar [{"text":"[ ","color":"yellow"},{"translate":"message.water","fallback":"水分 ","color":"aqua"},{"text":": ","color":"gray"},{"score":{"name":"@s","objective":"hs.water"},"color":"aqua","extra":[{"text":"%","color":"aqua"}]},{"text":" ]","color":"yellow"}]

### 喝水获得水分 ###
execute as @a if score @s hs.usedMilkBucket matches 1.. run function hardcore_survival:drink/drink
execute as @a if score @s hs.usedWaterBottle matches 1.. run function hardcore_survival:drink/drink

### 离着火生物过近造成伤害 ###
execute as @e at @s if score @s hs.fireTick matches 1.. run execute as @e[distance=..1.25] unless score @s hs.fireTick matches 1.. run scoreboard players add @s hs.fireTimer 1
execute as @e if score @s hs.fireTimer matches 30.. run data merge entity @s {Fire:100s}
execute as @e if score @s hs.fireTimer matches 30.. run scoreboard players set @s hs.fireTimer 0
execute as @e if score @s hs.fireTick matches 0 run scoreboard players set @s hs.fireTimer 0
execute as @e at @s if score @s hs.fireTick matches 1.. run execute as @a[distance=..1.25] unless score @s hs.fireTick matches 1.. run damage @s 1 in_fire

### 体力值效果 ###
execute as @a if score @s hs.sprintTick matches 0 if score @s hs.jumpTimes matches 0 if score @s hs.sneakTick matches 0 if score @s hs.exhaustion matches 2.. run scoreboard players remove @s hs.exhaustion 2
execute as @a if score @s hs.sprintTick matches 0 if score @s hs.jumpTimes matches 0 if score @s hs.sneakTick matches 1.. if score @s hs.exhaustion matches 2.. run scoreboard players remove @s hs.exhaustion 2
execute as @a if score @s hs.sprintTick matches 1.. run function hardcore_survival:player/exhaustion_sprint
execute as @a if score @s hs.jumpTimes matches 1.. run function hardcore_survival:player/exhaustion_jump
execute as @a if score @s hs.exhaustion matches 1200.. run function hardcore_survival:player/exhaustion_apply
execute as @a if score @s hs.exhaustion matches ..900 run function hardcore_survival:player/exhaustion_remove
execute as @a if score @s hs.exhaustion matches 1300.. run scoreboard players set @s hs.exhaustion 1300
scoreboard players set @s hs.sneakTick 0
